#!/usr/bin/env zsh
#
# Toggle system dark mode and run various related effects.
#

emulate -L zsh
setopt extended_glob warn_create_global typeset_silent \
        no_short_loops rc_quotes no_auto_pushd

local IS_DARK

.kitty-theme-change () {
  local dir="${XDG_CONFIG_HOME}/kitty"
  ln -sf "${dir}/${1}-theme.conf" "${dir}/theme.conf"
  kitty @ --to unix:/tmp/kitty-socket \
    set-colors -a -c "${dir}/theme.conf"
}

.emacs-theme-change () {
  emacsclient --no-wait --suppress-output --eval "(load-theme '$1 t)"
}

dark-mode
IS_DARK=$(is-dark-mode)

.kitty-theme-change ${${IS_DARK:+dark}:-light}
base16-set-theme ${${$(is-dark-mode):+${BASE16_THEME_DARK}}:-${BASE16_THEME_LIGHT}}
.emacs-theme-change ${${IS_DARK:+$CDOM_EMACS_THEME_DARK}:-$CDOM_EMACS_THEME_LIGHT}
